#  Configure EWAOL cloud-init configuration for using Azure control plane

do_install:append:azure-vm-arm64 () {
	sed -i '/ - mounts/d' ${D}/etc/cloud/cloud.cfg
	sed -i '/ - disk_setup/d' ${D}/etc/cloud/cloud.cfg
	sed -i '/cloud_init_modules/a\\ - mounts' ${D}/etc/cloud/cloud.cfg
	sed -i '/cloud_init_modules/a\\ - disk_setup' ${D}/etc/cloud/cloud.cfg

	echo "Allow only Azure datasource, disable fetching networking setting via IMDS"
	cat > ${D}/etc/cloud/cloud.cfg.d/91-azure_datasource.cfg <<EOF
datasource_list: [ Azure ]
datasource:
  Azure:
    apply_network_config: False
EOF
  cat >> ${D}/etc/cloud/cloud.cfg.d/05_logging.cfg <<EOF

# This tells cloud-init to redirect its stdout and stderr to
# 'tee -a /var/log/cloud-init-output.log' so the user can see output
# there without needing to look on the console.
output: {all: '| tee -a /var/log/cloud-init-output.log'}
EOF

	# Bake swap creation into the VM creation process
	cat > ${D}/etc/cloud/cloud.cfg.d/00-azure-swap.cfg << EOF
#cloud-config
# Generated by Azure cloud image build
disk_setup:
  ephemeral0:
    table_type: mbr
    layout: [66, [33, 82]]
    overwrite: True
fs_setup:
  - device: ephemeral0.1
    filesystem: ext4
  - device: ephemeral0.2
    filesystem: swap
mounts:
  - ["ephemeral0.1", "/mnt"]
  - ["ephemeral0.2", "none", "swap", "sw,nofail,x-systemd.requires=cloud-init.service,x-systemd.device-timeout=2", "0", "0"]
EOF

}

pkg_postinst:${PN} () {
#!/bin/sh -e
echo 'DefaultEnvironment="CLOUD_CFG=/etc/cloud/cloud.cfg.d/00-azure-swap.cfg"' >> $D/etc/systemd/system.conf
}

